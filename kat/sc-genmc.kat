let ppo = po
let sc = (ppo | rf | mo | fr | tc | tj | lin)

view porf = (po | rf | tc | tj | lin)+
view hb = porf
view hbRelinche = (po | rf;[NPLK] | tc | tj | lin)+ (genmc: "if (!getConf()->collectLinSpec && !getConf()->checkLinSpec) lab->addView({}); else")

export coherence hb

export acyclic sc

// ---------------------------------------------------------------------
// SC error detection
// ---------------------------------------------------------------------

let ww_conflict = [W] ; loc-overlap ; [W]
let wr_conflict = [W] ; loc-overlap ; [R] | [R] ; loc-overlap ; [W]
let conflicting = ww_conflict | wr_conflict
let na_conflict = [NA] ; conflicting | conflicting ; [NA]

export error VE_AccessNonMalloc unless  alloc <= hb
export error VE_DoubleFree      unless  [FREE|HPRET] ; loc-overlap ; [FREE|HPRET] = 0

export error VE_AccessFreed     unless  alloc^-1? ; free ; [FREE] <= hb
export error VE_AccessFreed     unless  [FREE] ; free^-1 ; alloc = 0
export error VE_AccessFreed     unless  [NOTHPPROT] ; alloc^-1? ; free ; [HPRET] <= hb
export error VE_AccessFreed     unless  [HPRET] ; free^-1 ; alloc ; [NOTHPPROT] = 0

export error VE_RaceNotAtomic   unless  na_conflict <= hb
export warning VE_WWRace        unless  ww_conflict <= porf
