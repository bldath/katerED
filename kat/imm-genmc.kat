// Extended coherence order
let eco = (rf | mo | fr)+

// ---------------------------------------------------------------------
// RC11 Happens-before
// ---------------------------------------------------------------------

// Optimized calculation of synchronizes-with
// Save the view of write messages

let relseq = [REL] ; ([F|TC] ; po)? ; (rf ; rmw)*
let sw_to_r = relseq ; rf ; [ACQ]
let sw_to_f = relseq ; rf ; po ; [F|TJ] ; [ACQ]
let sw = sw_to_r | sw_to_f
let asw = tc | tj | lin

assert sw = [REL] ; ([F|TC] ; po)? ; (rf ; rmw)* ; rf ; (po ; [F|TJ])? ; [ACQ]

// Happens-before
view hb = (po | sw_to_r | asw | sw_to_f)+

assert hb = (po | sw | asw)+

// Also save an 'abstract' hb that discards plock synchronization (relinche)
view hbRelinche = (po | tc | tj | lin
	| (relseq ; rf ; (po ; [F|TJ|TB])? ; [NPLK & ACQ]))+ (genmc: "if (!getConf()->collectLinSpec && !getConf()->checkLinSpec) lab->addView({}); else")

// ---------------------------------------------------------------------
// IMM Consistency axioms
// ---------------------------------------------------------------------

let bob = po ; [REL] | [ACQ] ; po | po ; [F] | [F] ; po | [W;REL]; po-loc ; [W]

let deps = data | ctrl | addr ; po? | rmw
let ppo = [DEP] ; (deps | rfi)+ ; [W] | bob | detour

// assert rmw <= ppo
// assert detour <= ppo
// assert [R]; po; [W&REL] <= ppo

// No OOTA
// acyclic (ppo | rf)

// Coherence : Optimize the checking of irreflexive (hb ; eco)
export coherence hb

// RMW atomicity
// check  mo-imm ; [UW] <= rf ; po-imm

// global ordering constraint
let FSC = [F] ; [SC]
let fhb = [F] ; hb
let hbf = hb ; [F]
let scb = po | rf | mo | fr
let psc = [SC] ; po ; hb ; po ; [SC]
        | [SC] ; fhb? ; scb ; hbf? ; [SC]
        | FSC ; hb ; FSC
        | FSC ; hb ; eco ; hb ; FSC
        | SC ; po ; SC
export acyclic psc unless [SC];any = 0

let ar = rfe | ppo | FSC ; hb ; eco ; hb ; FSC
export acyclic ar


// ---------------------------------------------------------------------
// IMM error detection
// ---------------------------------------------------------------------

let ww_conflict = [W] ; loc-overlap ; [W]
view porf = (po | rf | tc | tj | lin)+

export warning VE_WWRace        unless  ww_conflict <= porf